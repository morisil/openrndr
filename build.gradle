import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.10.1"
        classpath "com.netflix.nebula:nebula-publishing-plugin:17.3.0"
        classpath "com.netflix.nebula:nebula-release-plugin:15.1.0"
    }
}

plugins {
    // remember to update all the versions here when upgrading kotlin version
    id 'org.jetbrains.kotlin.jvm'                  version '1.4.31' apply false
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.4.31' apply false
}

apply plugin: 'org.jetbrains.dokka'

project.ext {
    kotlinVersion = '1.4.31'
    kotlinxSerializationVersion = '1.1.0' // check out kotlin releases to keep it in sync
    lwjglVersion = '3.2.3'
    javacpp_version = '1.5.4'
    ffmpeg_version = "4.3.1-$javacpp_version"
    spekVersion = '2.0.15'
    kluentVersion = '1.65'
    jsoupVersion = '1.13.1'
    kotestVersion = '4.4.3'
    junitJupiterVersion = '5.7.1'
}

dokka {
//
    outputDirectory = "$buildDir/docs"
    outputFormat = "html"
//    includes = ['Module.md']


    configuration {
        moduleName = "$rootProject.name"
        platform = "JVM"
        skipDeprecated = true
        includeNonPublic = false

        disableAutoconfiguration = false

        subProjects = ["openrndr-animatable", "openrndr-color", "openrndr-core", "openrndr-dialogs", "openrndr-event",
                       "openrndr-extensions", "openrndr-filter", "openrndr-math", "openrndr-shape", "openrndr-svg"]

        sourceLink {
            url = "https://github.com/openrndr/openrndr/blob/master/openrndr-core/src/main/kotlin"
            lineSuffix = "#L"
        }
        perPackageOption {
            prefix = "org.openrndr.internal"
            suppress = true
        }
        perPackageOption {
            prefix = "org.openrndr.shape.tessellation"
            suppress = true
        }
    }

    if (!System.properties['os.name'].toLowerCase().contains('windows')) {
        doLast {
            exec {
                executable "./dokka/postbuild.sh"
            }
        }
    }
}

allprojects {

    group 'org.openrndr'

    repositories {
        jcenter()
        maven {
            url "https://dl.bintray.com/spekframework/spek"
        }
    }
}

configure(allprojects.findAll { it.name != "openrndr-math" }) {
    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: 'kotlin'

    dependencies {
        // Note: kotlin-logging-jvm and kotlinx-coroutines-core are loaded
        // too early and their versions cannot be parametrized
        implementation 'io.github.microutils:kotlin-logging-jvm:2.0.4'
        implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.2'

        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"

        testImplementation "org.spekframework.spek2:spek-dsl-jvm:$spekVersion"
        testImplementation "org.amshove.kluent:kluent:$kluentVersion"
        testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlinVersion"
        testRuntimeOnly "org.spekframework.spek2:spek-runner-junit5:$spekVersion"
        testRuntimeOnly "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
    }

    test {
        useJUnitPlatform {
            includeEngines 'spek2'
        }
    }

    java {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    javadoc {
        options.addBooleanOption 'Xdoclint:none', true
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.AbstractKotlinCompile).all {
        kotlinOptions.freeCompilerArgs += ["-Xuse-experimental=kotlinx.coroutines.InternalCoroutinesApi"]
    }
}

configure(allprojects.findAll { it.name != "openrndr-demos" }) {
    apply plugin: 'nebula.contacts'

    // -- work-around for problem with nebula.info on arm64, which is currently not used a build host platform
    def arch = new DefaultNativePlatform("current").architecture.name
    if (arch != 'aarch64') {
        apply plugin: 'nebula.info'
    }
//    apply plugin: 'nebula.dependency-lock'

    apply plugin: 'nebula.release'
    // selectively apply only the bintray plugin, snapshots are not published
    // nebula-bintray needs to happened after nebula-release since version isn't lazy in the bintray extension

    apply plugin: 'nebula.maven-publish'
    apply plugin: 'nebula.source-jar'
    apply plugin: 'nebula.javadoc-jar'

    contacts {
        'edwin@openrndr.org' {
            moniker 'Edwin Jakobs'
            github 'edwinRNDR'
        }
    }
}

